#!/usr/bin/perl

use warnings;
use strict;

use File::Copy qw(move copy);
use Getopt::Std qw(getopts);

sub usage;
sub fatal;

$| = 1;

my %ext2coll = qw(
    .jpg    photos
    .jpeg   photos
    .avi    videos
    .mp4    videos
    .mpg    videos
    .mpeg   videos
);
my %coll2namer;

(my $prog = $0) =~ s{.+/}{};
my $root = $ENV{UFO_ROOT} ||= glob('~/var/ufo') || glob('~/.ufo') || fatal "Can't determine ufo root directory";
my %opt;

&{ __PACKAGE__->can(@ARGV ? 'cmd_' . shift : usage) || usage };

# --- Main functions

sub cmd_import {
    getopts(':h:xXr:vn', \%opt) or usage;
    $opt{x} = 1 if $opt{X};
    if (@ARGV) {
        # Nothing special to do
    }
    elsif (-t STDIN) {
        @ARGV = qw(.);
    }
    else {
        # Read files from standard input
        @ARGV = <STDIN>;
        chomp @ARGV;
    }
    my $hashf = $opt{h} || "/tmp/ufo.$$.sha256sum";
    if (!$opt{h} || ! -s $hashf) {
        print STDERR "Saving checksums to $hashf";
        calculate_hashes($hashf, @ARGV);
        print STDERR "\n";
    }
    open my $hashfh, '<', $hashf or fatal "open $hashf: $!";
    my (@imported, @already_imported);
    while (<$hashfh>) {
        my ($sum, $f) = split /\s+\*?/, $_, 2;
        chomp $f;
        print "> $f\n";
        my $ext = $f =~ m{(\.[^/]+)$} ? lc $1 : undef;
        my $coll = defined $ext ? $ext2coll{$ext} : undef;
        if (!defined $coll) {
            print "? no collection\n" if $opt{v};
            next;
        }
        my $namer = coll2namer($coll);
        my $m = $namer->($sum, $ext);
        if (-e $m) {
            print "- $m\n";
            push @already_imported, $f if $opt{X};
        }
        else {
            copy $f, $m or fatal "copy $f $m: $!" if !$opt{n};
            print "+ $m\n";
            push @imported, $f;
        }
    }
    continue {
        print "\n";
    }
    return if $opt{n} || !$opt{x};
    foreach my $f (@imported, @already_imported) {
        unlink $f;
    }
}

# --- Other functions

sub coll2namer {
    my ($coll) = @_;
    return $coll2namer{$coll} if defined $coll2namer{$coll};
    my $mroot = "$root/$coll";
    my @dirs2 = grep { m{/[0-9a-f]{2}$}i && -d } glob("$mroot/00/??");
    my @dirs1 = grep { m{/[0-9a-f]{2}$}i && -d } glob("$mroot/??");
    my $namer;
    if (@dirs2) {
        $namer = sub {
            my ($hash, $ext) = @_;
            sprintf '%s/%s/%s/%s%s', $mroot, substr($hash,0,2), substr($hash,2,2), $hash, $ext;
        }
    }
    elsif (@dirs1) {
        $namer = sub {
            my ($hash, $ext) = @_;
            sprintf '%s/%s/%s%s', $mroot, substr($hash,0,2), $hash, $ext;
        }
    }
    else {
        $namer = sub {
            my ($hash, $ext) = @_;
            sprintf '%s/%s%s', $mroot, $hash, $ext;
        }
    }
    return $coll2namer{$coll} = $namer;
}

sub calculate_hashes {
    my $hashf = shift;
    open my $hashfh, "| xargs -0 sha256sum > $hashf" or fatal "sha256sum: $!";
    while (@_) {
        my $f = shift @_;
        my $ext;
        if (-d $f) {
            unshift @_, glob("$f/*");
        }
        elsif (-f _) {
            print $hashfh $f, "\000";
        }
        else {
            print STDERR "not a regular file or directory: $f\n";
        }
    }
    close $hashfh or fatal "sha256sum: $!";
}

sub sha256hashes {
}

sub filetypes {
}

sub fatal {
    print STDERR $prog, ": @_\n";
    exit 1;
}
